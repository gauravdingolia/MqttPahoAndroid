apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }
}
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.upheo.android.mqtt'
            artifactId 'mqtt-client'
            version '1.0.0-snapshot4'
            artifact(sourceJar)
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }

    }

    repositories {
        if (project.hasProperty('upheo.maven.username')) {
            maven {
                url 'https://mymavenrepo.com/repo/i3Q7D532qLqzTEQkM8em/'
                credentials {
                    username project.getProperty('upheo.maven.username')
                    password project.getProperty('upheo.maven.password')
                }
            }
        } else {
            maven {
                url "$buildDir/repo"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'

    implementation "com.android.support:appcompat-v7:${version_support_library}"

    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.1"
    /* Logging */
    implementation group: 'com.jakewharton.timber', name: 'timber', version: '4.5.1'
    implementation group: 'com.jakewharton.timber', name: 'timber-lint', version: '3.1.0'
}
